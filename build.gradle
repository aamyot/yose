buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}


apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'

repositories {
    mavenCentral()
}

def libs = [
        molecule        : 'com.vtence.molecule:molecule:0.6.1',
        molecule_test   : 'com.vtence.molecule:molecule:0.6.1:tests',
        simple          : 'org.simpleframework:simple:5.1.6',
        jmustache       : 'com.samskivert:jmustache:1.9',
        hamcrest_all    : 'org.hamcrest:hamcrest-all:1.3',
        mockito         : 'org.mockito:mockito-all:1.10.19',
        junit           : 'junit:junit:4.11@jar',
        json_unit       : 'net.javacrumbs.json-unit:json-unit:1.5.1',
        gson            : 'com.google.code.gson:gson:2.3.1',
        no_log          : 'org.slf4j:slf4j-nop:1.7.10'
]

dependencies {
    compile libs.molecule
    compile libs.no_log
    compile libs.simple
    compile libs.jmustache
    compile libs.gson
    testCompile libs.molecule_test
    testCompile libs.hamcrest_all
    testCompile libs.junit
    testCompile libs.json_unit
    testCompile libs.mockito
}

mainClassName = "com.alexandreamyot.yose.Yose"
applicationName = 'yose'

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

if (!project.hasProperty('port')) {
    ext.port = 8080
}

run {
    args port
}

task stage(dependsOn: ['clean', 'installApp'])

task coverage(dependsOn: ['clean', 'test', 'jacocoTestReport'])
